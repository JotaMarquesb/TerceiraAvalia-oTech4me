unit uGerenciadorContas;

interface

uses
  System.Generics.Collections, uContaBancaria;

type
  TGerenciadorContas = class
  private
    FContas: TObjectList<TContaBancaria>;
    FUltimoNumero: Integer;

    function ExisteCPF(const ACPF: string): Boolean;
  public
    constructor Create;
    destructor Destroy; override;

    function CriarConta(const ATitular, ACPF: string): TContaBancaria;
    function LoginConta(const ANumeroConta: string): TContaBancaria;
  end;

implementation

uses
  System.SysUtils;

constructor TGerenciadorContas.Create;
begin
  FContas := TObjectList<TContaBancaria>.Create(True);
  FUltimoNumero := 0;
end;

destructor TGerenciadorContas.Destroy;
begin
  FContas.Free;
  inherited;
end;

function TGerenciadorContas.ExisteCPF(const ACPF: string): Boolean;
var
  Conta: TContaBancaria;
begin
  Result := False;
  for Conta in FContas do
    if Conta.CPF = ACPF then
      Exit(True);
end;

function TGerenciadorContas.CriarConta(const ATitular, ACPF: string): TContaBancaria;
var
  NumeroGerado: string;
begin
  if ExisteCPF(ACPF) then
    raise Exception.Create('CPF já cadastrado!');

  Inc(FUltimoNumero);
  NumeroGerado := FormatFloat('0000', FUltimoNumero);

  Result := TContaBancaria.Create(NumeroGerado, ATitular, ACPF);

  FContas.Add(Result);
end;

function TGerenciadorContas.LoginConta(const ANumeroConta: string): TContaBancaria;
var
  Conta: TContaBancaria;
begin
  Result := nil;

  for Conta in FContas do
    if Conta.NumeroConta = ANumeroConta then
      Exit(Conta);
end;

end.
